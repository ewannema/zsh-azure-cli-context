#compdef azctx

# Set default contexts directory with fallback
: "${ZSH_AZCTX_CONTEXTS_DIR:="${HOME}/.azure-contexts"}"

_azctx() {
  typeset state line
  typeset -i ret

  ret=1

  _arguments -C \
    '1: :_azctx_commands' \
    '*:: :->args' && ret=0

    case $state in
      args)
        case ${words[1]} in
          help|list|reset)
            # No completion needed for these commands
            ;;
          new)
            _message 'context name' && ret=0
            ;;
          rm|use)
            _azctx_contexts && ret=0
            ;;
          run)
            if (( CURRENT == 2 )); then
              _azctx_contexts && ret=0
            else
              shift words
              (( CURRENT-- ))
              _normal && ret=0
            fi
            ;;
        esac
    esac

  # shellcheck disable=SC2152
  return ret
}

_azctx_commands() {
    local -a commands=(
        "help:Show help"
        "list:List available contexts"
        "new:Make a new context"
        "reset:Reset context settings"
        "rm:Remove a context"
        "run:Run a command in a context"
        "use:Switch to a context"
    )

    _describe -t commands 'command' commands "$@"
}

_azctx_contexts() {
  local -a contexts

  contexts=("${ZSH_AZCTX_CONTEXTS_DIR}"/*(N:t))

  if (( ${#contexts} == 0 )); then
    _message 'no contexts available'
    return 1
  fi

  contexts=("${(on)contexts[@]}")

  _describe 'context' contexts
}

_azctx "$@"
